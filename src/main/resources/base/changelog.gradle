// no parameters needed, just apply `com.diffplug.spotless-changelog` on whichever project has
// CHANGELOG.md
//
// this script will look for the changelog tasks in either this project or the parent project,
// and then set the version from the spotless-changelog plugin. It will also hook the jar task
// (if present) to depend on changelogCheck, so that a jar will never get published with a bad
// changelog.  Lastly, changelogBump will depend on a successful bintrayUpload/bintrayPublish.

if (tasks.names.contains('changelogCheck')) {
	// set the project version for POM, jar manifest, etc.
	version = spotlessChangelog.versionNext
	// ensures that nothing will be built if changelogPush will end up failing
	afterEvaluate {
		if (tasks.names.contains('jar')) {
			tasks.named('jar').configure {
				dependsOn tasks.named('changelogCheck')
			}
		}
	}
	// ensures that changelog bump and push only happens if the publish was successful
	tasks.named('changelogBump').configure {
		if (tasks.names.contains('bintrayUpload')) {
			dependsOn tasks.named('bintrayUpload')
			// only the root project has bintrayPublish, and it finalizes all bintrayUpload tasks
			// https://github.com/bintray/gradle-bintray-plugin/blob/3fe02dfdae3e807afba57e0140a0d4c2424674e1/src/main/groovy/com/jfrog/bintray/gradle/ProjectsEvaluatedBuildListener.groovy#L37-L41
			dependsOn rootProject.tasks.named('bintrayPublish')
		}
		// if we have a gradle plugin, we need to push it up to the plugin portal too
		if (tasks.names.contains('publishPlugins')) {
			dependsOn tasks.named('publishPlugins')
		}
	}
} else {
	assert parent != null : 'If you apply this script to the root project, you must have plugin com.diffplug.spotless-changelog'
	assert parent.tasks.names.contains('changelogCheck') : 'Neither this project nor its parent had the required plugin com.diffplug.spotless-changelog'

	// same as above, but pull changelog stuff from the parent changelog
	version = parent.spotlessChangelog.versionNext
	afterEvaluate {
		if (tasks.names.contains('jar')) {
			tasks.named('jar').configure {
				dependsOn parent.tasks.named('changelogCheck')
			}
		}
	}
	parent.tasks.named('changelogBump').configure {
		if (tasks.names.contains('bintrayUpload')) {
			dependsOn tasks.named('bintrayUpload')
			dependsOn rootProject.tasks.named('bintrayPublish')
		}
		if (tasks.names.contains('publishPlugins')) {
			dependsOn tasks.named('publishPlugins')
		}
	}
}
